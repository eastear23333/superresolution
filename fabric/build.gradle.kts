import multiversion.VersionConfig
import org.gradle.language.jvm.tasks.ProcessResources
import utils.MinecraftVersion

plugins {
    id("multiloader-loader")
    id("net.fabricmc.fabric-loom-remap") version "1.16.3"
}

@Suppress("UNCHECKED_CAST")
val versionConfig = rootProject.extra["versionConfig"] as VersionConfig
val isDevBuild = gradle.extensions.extraProperties["isDev"] as? Boolean ?: false
val imguiVersion = if (MinecraftVersion.of(versionConfig.common.minecraftVersion) >= MinecraftVersion.of("26.1")) "1.92.0" else "1.90.0"

fun findFirstConfiguration(vararg names: String): String {
    return names.firstOrNull { name -> configurations.findByName(name) != null } ?: names.last()
}

fun DependencyHandler.mappingsCompat(notation: Any) =
    add(findFirstConfiguration("mappings"), notation)

fun DependencyHandler.modImplementationCompat(notation: Any) =
    add(findFirstConfiguration("modImplementation", "implementation"), notation)

fun DependencyHandler.modCompileOnlyCompat(notation: Any) =
    add(findFirstConfiguration("modCompileOnly", "compileOnly"), notation)

repositories {
    maven {
        url = uri("https://cursemaven.com")
        content {
            includeGroup("curse.maven")
        }
    }
    maven {
        name = "Terraformers"
        url = uri("https://maven.terraformersmc.com")
    }
    maven {
        name = "Modrinth"
        url = uri("https://api.modrinth.com/maven")
        content {
            includeGroup("maven.modrinth")
        }
    }
    maven {
        name = "yoga"
        url = uri("https://repo1.maven.org/maven2")
        content {
            includeGroup("org.appliedenergistics.yoga")
        }
    }
    maven {
        name = "CaffeineMC"
        url = uri("https://maven.caffeinemc.net/releases" )
        mavenContent { releasesOnly() }
        content {
            includeGroup("net.caffeinemc")
        }
    }

    maven {
        name = "CaffeineMC"
        url = uri("https://maven.caffeinemc.net/snapshots")
        mavenContent { snapshotsOnly() }
        content {
            includeGroup("net.caffeinemc")
        }
    }
}

dependencies {
    minecraft("com.mojang:minecraft:${versionConfig.common.minecraftVersion}")
    if (MinecraftVersion.of(versionConfig.common.minecraftVersion) < MinecraftVersion.of("26.1")) {
        mappingsCompat(
            loom.layered {
                officialMojangMappings()
                if (versionConfig.common.parchmentVersion != null) {
                    parchment("org.parchmentmc.data:parchment-${versionConfig.common.parchmentVersion}@zip")
                }
            }
        )
    }

    modImplementationCompat("net.fabricmc:fabric-loader:${versionConfig.fabric.loaderVersion}")
    modImplementationCompat("net.fabricmc.fabric-api:fabric-api:${versionConfig.fabric.apiVersion}")

    if (versionConfig.common.architecturyApiVersion != null) {
        modImplementationCompat("dev.architectury:architectury-fabric:${versionConfig.common.architecturyApiVersion}")
    }
    if (versionConfig.common.clothConfigVersion != null) {
        modImplementationCompat("me.shedaniel.cloth:cloth-config-fabric:${versionConfig.common.clothConfigVersion}")
    }
    if (versionConfig.fabric.modmenuVersion != null) {
        modImplementationCompat("com.terraformersmc:modmenu:${versionConfig.fabric.modmenuVersion}")
    }

    implementation("net.lenni0451:Reflect:1.3.4")
    val imguiAppDep = implementation("io.github.spair:imgui-java-app:$imguiVersion")
    if (isDevBuild && imguiAppDep != null) include(imguiAppDep)

    val imguiBindingDep = implementation("io.github.spair:imgui-java-binding:$imguiVersion")
    if (isDevBuild && imguiBindingDep != null) include(imguiBindingDep)

    val imguiLwjglDep = implementation("io.github.spair:imgui-java-lwjgl3:$imguiVersion")
    if (isDevBuild && imguiLwjglDep != null) include(imguiLwjglDep)
    implementation("org.anarres:jcpp:1.4.14")
    implementation("org.antlr:antlr4-runtime:4.13.1")

    val busDep = implementation("net.neoforged:bus:8.0.5")
    if (busDep != null) include(busDep)

    val typetoolsDep = implementation("net.jodah:typetools:0.6.3")
    if (typetoolsDep != null) include(typetoolsDep)

    if (MinecraftVersion.of(versionConfig.common.minecraftVersion) < MinecraftVersion.of("1.21.8")) {
        implementation("io.github.douira:glsl-transformer:2.0.1")
    } else {
        implementation("io.github.douira:glsl-transformer:3.0.0-pre3")
    }
    if (MinecraftVersion.of(versionConfig.common.minecraftVersion) < MinecraftVersion.of("26.2")) {
        implementation("org.lwjgl:lwjgl-vulkan:${versionConfig.common.lwjglVersion}")?.let { include(it) }
        implementation("org.lwjgl:lwjgl-vma:${versionConfig.common.lwjglVersion}")?.let { include(it) }
        implementation("org.lwjgl:lwjgl-vma::natives-windows")?.let { include(it) }
        implementation("org.lwjgl:lwjgl-vma::natives-linux")?.let { include(it) }
    }

    implementation("com.electronwill.night-config:toml:3.8.3")?.let { include(it) }
    implementation("com.electronwill.night-config:core:3.8.3")?.let { include(it) }

    for (lib in versionConfig.fabric.dependencies.modrinth) {
        var depName = "maven.modrinth:${lib.name}:${lib.version}-fabric,${lib.minecraftVersion ?: versionConfig.common.minecraftVersion}"
        if ((lib.name == "sodium" && MinecraftVersion.of(versionConfig.common.minecraftVersion) > MinecraftVersion.of("1.21.10")) || lib.name == "sodium.maven") {
            depName = "net.caffeinemc:sodium-fabric:${lib.version}"
            if (lib.compileOnly) {
                modCompileOnlyCompat(depName)
            } else {
                modImplementationCompat(depName)
            }
            continue
        }

        if (lib.compileOnly) {
            if (lib.useJarJar) {
                modCompileOnlyCompat(utils.JarJar.extractJars(project, depName))
            } else {
                modCompileOnlyCompat(depName)
            }
        } else {
            if (lib.useJarJar) {
                modImplementationCompat(utils.JarJar.extractJars(project, depName))
            } else {
                modImplementationCompat(depName)
            }
        }
    }

    for (lib in versionConfig.fabric.dependencies.curseforge) {
        val depName = lib.curseMavenNotation()
        if (lib.compileOnly) {
            if (lib.useJarJar) {
                modCompileOnlyCompat(utils.JarJar.extractJars(project, depName))
            } else {
                modCompileOnlyCompat(depName)
            }
        } else {
            if (lib.useJarJar) {
                modImplementationCompat(utils.JarJar.extractJars(project, depName))
            } else {
                modImplementationCompat(depName)
            }
        }
    }

    for (lib in versionConfig.fabric.dependencies.local) {
        if (lib.isMod) {
            if (lib.compileOnly) {
                if (lib.useJarJar) {
                    modCompileOnlyCompat(utils.JarJar.extractJars(project, "../libs/${lib.name}.jar"))
                } else {
                    modCompileOnlyCompat(files("../libs/${lib.name}.jar"))
                }
            } else {
                if (lib.useJarJar) {
                    modImplementationCompat(utils.JarJar.extractJars(project, "../libs/${lib.name}.jar"))
                } else {
                    modImplementationCompat(files("../libs/${lib.name}.jar"))
                }
            }
        } else {
            if (lib.compileOnly) {
                compileOnly(files("../libs/${lib.name}.jar"))
            } else {
                implementation(files("../libs/${lib.name}.jar"))
            }
        }
    }
}

loom {
    mixin {
        if(MinecraftVersion.of(versionConfig.common.minecraftVersion) < MinecraftVersion.of("26.1")) {
            useLegacyMixinAp = true
            defaultRefmapName.set("${rootProject.property("mod_id")}.refmap.json")
        }
    }
    runs {
        named("client") {
            vmArg("-XX:+CreateMinidumpOnCrash")

            vmArg("--enable-preview")
            vmArg("--enable-native-access=ALL-UNNAMED")

            vmArg("-Dmixin.debug.export=true")

            // We force set the lwjgl version to versionConfig.common.lwjglVersion
            // Sodium will check the lwjgl version,ensure it equals the lwjgl version of corresponding Minecraft version
            // We must disable the check,otherwise sodium will crash the game.
            vmArg("-Dsodium.checks.issue2561=false")

            // Only effective in 1.21.5+
            programArg("--graphicsBackend=opengl")
            programArg("--renderDebugLabels=true")

            ideConfigGenerated(true)
            runDir("../runs/fabric")
        }
    }
}

val fabricModVersion = project.version.toString()
val fabricJavaVersion = versionConfig.common.javaVersion.toString()
val fabricVersionRange = if (versionConfig.common.fabricVersionRange.size == 1) {
    "\"${versionConfig.common.fabricVersionRange[0]}\""
} else {
    versionConfig.common.fabricVersionRange.joinToString(prefix = "[", postfix = "]") { "\"$it\"" }
}

tasks.named<ProcessResources>("processResources") {
    inputs.property("version", fabricModVersion)
    inputs.property("javaVersion", fabricJavaVersion)
    inputs.property("versionRange", fabricVersionRange)
    filesMatching("fabric.mod.json") {
        expand(
            mapOf(
                "version" to fabricModVersion,
                "javaVersion" to fabricJavaVersion,
                "versionRange" to fabricVersionRange
            )
        )
        filter { line: String ->
            line.replace("\"{versionRange}\"", fabricVersionRange)
        }
    }
    if (gradle.extensions.extraProperties.properties["isUseDebugLib"] as? Boolean == true){
        exclude("**/libSuperResolution*+*+release.*")
    } else {
        exclude("**/libSuperResolution*+*+debug.*")
    }
}

tasks.matching { it.name == "remapSourcesJar" }.configureEach {
    enabled = false
}
